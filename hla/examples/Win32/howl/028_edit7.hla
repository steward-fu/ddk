// pgm_edit7-
//
//	This program demonstrates the use of edit boxes on a form.

program pgm_edit7;
#linker( "comdlg32.lib" ) 
#linker( "comctl32.lib" )

?@NoDisplay 	:= true;
?@NoStackAlign	:= true;

#includeOnce( "stdlib.hhf" )
#includeOnce( "howl.hhf" )

const
	applicationName := "Edit #7 (alphabetic editBox)";
	formX			:= w.CW_USEDEFAULT;	// Let Windows position this guy
	formY			:= w.CW_USEDEFAULT;
	formW			:= 600;
	formH			:= 600;

	
// Forward declarations for the onClick widgetProcs that we're going to
// call when an event occurs.

proc hideShowEditBox		:widgetProc; @forward;
proc moveEditBox			:widgetProc; @forward;
proc resizeEditBox			:widgetProc; @forward;
proc onEditUpdate		   	:widgetProc; @forward;
proc onQuit			   		:widgetProc; @forward;



// Here's the main form definition for the app:

wForm( mainAppWindow );

	var
		showState	:boolean;
		align(4);
	
	
	wEditBox
	(
		editBox1,							// editBox name
		"",									// Initial text string
		10,									// x
		10,									// y
		200,								// width
		30,									// height
		0,									// style
		NULL								// on change handler 
	)
	
	wPushButton
	(
		button2,					// Field name in mainWindow object
		"Hide editBox",				// Caption for push button
		250,						// x position 
		10,							// y position 
		175,						// width 
		25,							// height 
		hideShowEditBox				// initial "on click" event handler 
	)
	
	
	wPushButton
	(
		button3,					// Field name in mainWindow object
		"Move EditBox",				// Caption for push button
		250,						// x position 
		40,							// y position 
		175,						// width 
		25,							// height 
		moveEditBox			   		// initial "on click" event handler 
	)
	
	
	wPushButton
	(
		button4,					// Field name in mainWindow object
		"Resize EditBox",			// Caption for push button
		250,						// x position 
		70,							// y position 
		175,						// width 
		25,							// height 
		resizeEditBox			   	// initial "on click" event handler 
	)
	
		
	
	
	
	// Place a quit button in the lower-right-hand corner of the form:
	
	wPushButton
	(
		quitButton,				// Field name in mainWindow object
		"Quit",					// Caption for push button
		450,					// x position 
		525,					// y position 
		125,					// width 
		25,						// height 
		onQuit					// "on click" event handler 
	)
	
endwForm


// Must invoke the following macro to emit the code generated by
// the wForm macro:

mainAppWindow_implementation();
	




	




// The moveEditBox widget proc will move the editBox 
// between y positions 10 and 40.

proc moveEditBox:widgetProc;
begin moveEditBox;

	mov( mainAppWindow.editBox1, esi );
	(type wEditBox_t [esi]).get_y();
	if( eax = 10 ) then
	
		stdout.put( "Moving editBox to y-position 40" nl );
		(type wEditBox_t [esi]).set_y( 40 );
		
	else
	
		stdout.put( "Moving editBox to y-position 10" nl );
		(type wEditBox_t [esi]).set_y( 10 );
		
	endif;
	
end moveEditBox;
	


// The hideShowEditBox widget proc will hide and show editBox1.

proc hideShowEditBox:widgetProc;
begin hideShowEditBox;

	mov( thisPtr, esi );
	if( mainAppWindow.showState ) then
	
		(type wPushButton_t [esi]).set_text( "Hide EditBox" );
		mov( false, mainAppWindow.showState );
		stdout.put( "Showing editBox 1" nl );
		
		mov( mainAppWindow.editBox1, esi );
		(type wEditBox_t [esi]).show();
		
	else		 
	
		(type wPushButton_t [esi]).set_text( "Show EditBox" );
		mov( true, mainAppWindow.showState );
		stdout.put( "Hiding editBox 1" nl );
		
		mov( mainAppWindow.editBox1, esi );
		(type wEditBox_t [esi]).hide();
				
	endif;	
	
end hideShowEditBox;
	


	


// The resizeEditBox widget proc change the size of the edit box.

proc resizeEditBox:widgetProc;
static
	state:boolean := true;
	
begin resizeEditBox;

	mov( mainAppWindow.editBox1, esi );
	if( state ) then
	
		mov( false, state );
		(type wEditBox_t [esi]).resize
		(
			150,
			30
		);
		
	else
	
		(type wEditBox_t [esi]).resize
		(
			200,
			30
		);
		mov( true, state );
		
	endif;
		
end resizeEditBox;
	


	
// Here's the onUpdate event handler the edit box object:

proc onEditUpdate:widgetProc;
var
	textEntry	:string;
	startEdit	:dword;
	endEdit		:dword;
	
begin onEditUpdate;

	mov( thisPtr, esi );
	
	// Get the selection so that we know the cursor position:
	
	(type wEditBox_t [esi]).get_selection( startEdit, endEdit );

	(type wEditBox_t [esi]).a_get_text();
	mov( eax, textEntry );
	mov( eax, esi );
	mov( eax, edi );
	xor( ecx, ecx );
	xor( edx, edx );
	while( (type byte [esi]) <> 0 ) do
	
		mov( [esi], al );
		chars.toLower( al );
		if( al not in 'a'..'z' ) then
		
			push( edx );
			push( ecx );
			w.MessageBeep( -1 );
			stdout.put( "Bad char: ", (type char [esi]), nl );
			pop( ecx );
			pop( edx );
			
		else
		
			mov( al, [edi] );
			inc( edi );				// Bump dest ptr
			inc( ecx );				// Bump up new length
			
		endif;
		inc( edx );					// Bump up original length
		inc( esi );					// Bump src ptr
	
	endwhile;
	if( esi <> edi ) then
	
		// There were bad characters, deal with that here:
		//
		// Set the length of the new string:
		
		mov( textEntry, eax );
		mov( ecx, (type str.strRec [eax]).length );
		
		// Zero-terminate the new string:
		
		mov( 0, (type byte [eax+ecx]));
		
		// Send the new string (minus invalid chars) tot he control:
		
		mov( mainAppWindow.editBox1, esi );
		(type wEditBox_t [esi]).set_text( textEntry );
		
	endif;
	str.free( textEntry );
	
	// Restore the caret position:
	
	mov( thisPtr, esi );
	sub( ecx, edx );
	mov( endEdit, eax );
	sub( edx, eax );
	(type wEditBox_t [esi]).set_selection( eax, eax );
	
	
end onEditUpdate;


	

// Here's the onClick event handler for our quit button on the form.
// This handler will simply quit the application:

proc onQuit:widgetProc;
begin onQuit;

	// Quit the app:
	
	w.PostQuitMessage( 0 );

end onQuit;





// We'll use the main application form's onCreate method to initialize
// the various buttons on the form.
//
// This could be done in appStart, but better to leave appStart mainly
// as boilerplate code. Also, putting this code here allows us to use
// "this" to access the mainAppWindow fields (a minor convenience).
		
method mainAppWindow_t.onCreate;
begin onCreate;

	// Initialize the showState data field:
		
	mov( false, this.showState );
	
	// onUpdate is like onChange, except we get to intercept the
	// change to the edit box before the text is drawn to the window.
	
	mov( mainAppWindow.editBox1, esi );
	(type wEditBox_t [esi]).set_onUpdate( &onEditUpdate );
	

end onCreate;

	
	
///////////////////////////////////////////////////////////////////////////////
//
//
// The following is mostly boilerplate code for all apps (about the only thing
// you would change is the size of the main app's form)
//
//
///////////////////////////////////////////////////////////////////////////////
//	
// When the main application window closes, we need to terminate the 
// application. This overridden method handles that situation.  Notice the
// override declaration for onClose in the wForm declaration given earlier.
// Without that, mainAppWindow_t would default to using the wVisual_t.onClose
// method (which does nothing). 
		
method mainAppWindow_t.onClose;
begin onClose;
	
	// Tell the winmain main program that it's time to terminate.
	// Note that this message will (ultimately) cause the appTerminate
	// procedure to be called.
	
	w.PostQuitMessage( 0 );
	
	
end onClose;

			   
  
			   
   
// When the application begins execution, the following procedure
// is called.  This procedure must create the main
// application window in order to kick off the execution of the
// GUI application:
  
procedure appStart;
begin appStart;

	push( esi );
	
	// Create the main application window:
	
	mainAppWindow.create_mainAppWindow
	(
		applicationName,		// Window title
		w.WS_EX_CONTROLPARENT,	// Need this to support TAB control selection
		w.WS_OVERLAPPEDWINDOW,	// Style 
		NULL,					// No parent window 									
		formX,					// x-coordinate for window. 
		formY,					// y-coordinate for window.
		formW,					// Width
		formH,					// Height
		howl.bkgColor_g,		// Background color
		true					// Make visible on creation 
	);
	mov( esi, pmainAppWindow );	// Save pointer to main window object.
	pop( esi );

end appStart;



// appTerminate-
//
//	Called when the application is quitting, giving the app a chance
// to clean up after itself.
//
// Note that this is called *after* the mainAppWindow_t.onClose method
// executes (indeed, mainAppWindow_t.onClose, by posting the quit message,
// is what actually causes the program to begin terminating, which leads
// to the execution of this procedure).

procedure appTerminate;
begin appTerminate;
	
	// Clean up the main application's form.
	// Note that this will recursively clean up all the widgets on the form.
	
	mainAppWindow.destroy();
	
end appTerminate;


// appException-
//
// Gives the application the opportunity to clean up before
// aborting when an unhandled exception comes along:

procedure appException
( 
	theException	:dword in eax;
	raiseAdrs		:dword in ebx;
	filename		:string in ecx;
	userCode		:dword in edx;
	lineNum			:uns32 in edi 
);
begin appException;

	raise( eax );

end appException;



// The main program for a HOWL application must simply
// call the HowlMainApp procedure.

begin pgm_edit7;

	// Set up the background and transparent colors that the
	// form will use when registering the window_t class:
	
	w.GetSysColor( w.COLOR_MENU );
	mov( eax, howl.bkgColor_g );
	or( $FF00_0000, eax );
	mov( eax, howl.transparent_g );
	w.CreateSolidBrush( howl.bkgColor_g );
	mov( eax, howl.bkgBrush_g );

	// Start the HOWL Framework Main Program:
	
	HowlMainApp();
	
	// Delete the brush we created earlier:
	
	w.DeleteObject( howl.bkgBrush_g );			
				
end pgm_edit7;
 